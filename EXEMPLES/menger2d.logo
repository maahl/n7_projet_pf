DEF menger (n, l)
BEGIN
	IF (n>0) THEN
	BEGIN
        	(* affichage du carre *)
		REPEAT (4)
		BEGIN
			MOVE (l)
			ROTATE (90)
		END

	        (* affichage des 3 sous-carres du bas *)
        	REPEAT (3)
	        BEGIN
        		CALL menger ((n-1), (l/3))
            		JUMP (l/3)
        	END
        	(* retour au point d'origine, pour ne pas perturber les appels precedents a la fonction *)
	        ROTATE (180)
        	MOVE (l)
	        ROTATE (180)

        	(* on monte d'une ligne *)
	        ROTATE (90)
        	MOVE (l/3)
	        ROTATE (-90)

        	(* on affiche le carre milieu-gauche *)
	        CALL menger ((n-1), (l/3))
        	(* on se place au niveau du carre milieu droit *)
	        JUMP ((2*l)/3)
        	(* on affiche le carre milieu droit *)
	        CALL menger ((n-1), (l/3))

		(* on retourne a l'origine *)
		ROTATE (180)
		JUMP ((2*l)/3)
		ROTATE (90)
		JUMP (l/3)
		ROTATE (90)

		(* on monte de 2 lignes *)
		ROTATE (90)
		JUMP ((2*l)/3)
		ROTATE (-90)

		(* on affiche les 3 carres du haut *)
		REPEAT (3)
		BEGIN
			CALL menger ((n-1), (l/3))
			JUMP (l/3)
		END
		(* Retour a l'origine *)
		ROTATE (180)
		JUMP (l)
		ROTATE (90)
		JUMP ((2*l)/3)
		ROTATE (90)
	END
	ELSE
	BEGIN
	END
END
BEGIN
	CALL menger ((5), (600))
END
